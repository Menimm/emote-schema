
plugins {
  id 'com.github.jacobono.jaxb' version '1.3.5'
  id "com.jfrog.bintray" version "1.7"
}


apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.github.jacobono.jaxb'


group = 'com.halkow.emote'
version = '0.0.0'


repositories {
  mavenLocal()
  jcenter()
  maven {
    url 'http://dl.bintray.com/hendrikhalkow/maven'
  }
}


publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
}


bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['mavenJava']
  dryRun = false
  publish = true

  pkg {
    repo = 'maven'
    name = 'emote-schema'
    desc = 'Emote schema definitions'
    websiteUrl = 'https://github.com/hendrikhalkow/emote-schema'
    issueTrackerUrl = 'https://github.com/hendrikhalkow/emote-schema/issues'
    vcsUrl = 'https://github.com/hendrikhalkow/emote-schema.git'
    licenses = ['GPL-3.0']
    publicDownloadNumbers = true
    version {
      name = project.version //Bintray logical version name
      desc = 'optional, version-specific description'
    }
  }
}


dependencies {
  jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics-ant:0.11.1'
  jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics:0.11.1'
  jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics-annotate:1.0.2'
  jaxb 'com.sun.xml.bind:jaxb-core:2.2.11'
  jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.11'
  jaxb 'com.sun.xml.bind:jaxb-impl:2.2.11'
  jaxb 'javax.xml.bind:jaxb-api:2.2.12'
  compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.21'
}


jaxb {
  xsdDir      = "src/main/xsd"
  bindingsDir = "src/main/xjb"
  bindings = ["ENRC.xjb"]
  episodesDir = "build/episodes"
  xjc {
     taskClassname      = 'org.jvnet.jaxb2_commons.xjc.XJC2Task'
     //generatePackage    = 'com.halkow.emote.schema'
     args               = ['-Xinheritance', '-Xannotate']
     destinationDir     = 'build/generated-src'
  }
}


compileJava {
  dependsOn xjc
  source "${buildDir}/generated-src"
}
